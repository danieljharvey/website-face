module Maybe where

import Prelude hiding (Maybe(..))

data Maybe a = Just a | Nothing
  deriving (Eq, Ord, Show)

-- If we have a value, run the function on it
-- if we don't - do nothing
instance Functor Maybe where
  fmap f (Just a) = Just (f a)
  fmap _ Nothing  = Nothing

-- pure = default for datatype.
-- we assume success so Just
-- <*> is for combining functions that are also in Maybe
instance Applicative Maybe where
  pure a = Just a
  (Just f) <*> (Just a) = Just (f a)
  _        <*> _        = Nothing

-- if we get two wrapped maybes, make it into one
-- if we start with Nothing, do nothing else
instance Monad Maybe where
  (Just a) >>= k   = k a
  Nothing  >>= _   = Nothing

-- concatenation of things - the things must themselves be combinable
instance (Semigroup a) => Semigroup (Maybe a) where
  (Just a) <> (Just b) = Just (a <> b)
  Nothing  <> a        = a
  a        <> Nothing  = a

-- empty value is nothing as adding nothing to anything does not change it
instance (Semigroup a) => Monoid (Maybe a) where
  mempty = Nothing

-- 
instance Foldable Maybe where
  foldr _ a Nothing  = a
  foldr f a (Just b) = f b a
